apply plugin: 'com.android.application'


def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.adasleader.jason.adasleader"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 218030502
        versionName "1.4.10"
    }

    signingConfigs {
        release {
            //storeFile file('D:/AndroidOutput/Key/ADASLeader.jks')
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }
/*
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
*/
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    output.outputFileName = "ADASLeader_${variant.versionCode}_${variant.flavorName}_${variant.buildType.name}.apk"
                }
            }

            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "mode"

    productFlavors {
        pro {
            dimension "mode"
            applicationIdSuffix ".pro"
        }
        sale {
            dimension "mode"
        }
    }

    variantFilter{ variant->
        def names = variant.flavors*.name
        if (names.contains("pro") && variant.buildType.name=="release"){
            setIgnore(true)
        }
    }

}

repositories {
    maven {
        url "https://jitpack.io"
    }
    google()
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:27.1.0'
    compile 'com.android.support:recyclerview-v7:27.1.0'
    compile 'com.android.support:cardview-v7:27.1.0'
    compile 'com.android.support:support-v4:27.1.0'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.2'
}

